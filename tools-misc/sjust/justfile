set allow-duplicate-recipes := false
set ignore-comments := true

_default:
	#!/usr/bin/env bash
	/usr/bin/just --list --list-heading $'Available commands:\n' --list-prefix $' - '

download-additional-wallpaper:
	#!/usr/bin/env bash
	rm -rf ~/Pictures/Wallpaper
	wget -r -np -P ~/Pictures --reject="*index.html*" --reject=".*Phone.*" https://dufs-prod-1.casa.gierth.nl/Wallpaper/ --no-host-directories

reset-user-custom-keybindings:
	#!/usr/bin/env bash
	dconf reset -f /org/gnome/settings-daemon/plugins/media-keys/custom-keybindings/
	dconf reset -f /org/gnome/settings-daemon/plugins/media-keys/custom-keybindings

reboot-to-bios:
	#!/usr/bin/env bash
	if [ -d /sys/firmware/efi ]; then
		systemctl reboot --firmware-setup
	else
		echo "Rebooting to legacy BIOS from OS is not supported."
	fi

logs-this-boot:
	#!/usr/bin/env bash
	sudo journalctl --no-hostname -b 0

logs-last-boot:
	#!/usr/bin/env bash
	sudo journalctl --no-hostname -b -1


system-update:
	#!/usr/bin/env bash
	echo "== Update Fisher =="
	fish -c "source /usr/share/fish/vendor_conf.d/fisher.fish; if type -q fisher; if string length --quiet (fisher list); fisher update >/dev/null; end; end"
	echo "== Update Mise Tools =="
	mise upgrade --bump
	echo "== Update Flatpak Applications =="
	flatpak update --assumeyes --noninteractive
	echo "== Update System Image =="
	sudo bootc upgrade

	# Prompt user to quit or reboot
	while true; do
		read -n 1 -s -r -p $'\e[32m(Q)uit,\e[0m\e[31m (R)eboot, (P)oweroff \e[0m' key
		if [[ $key == "q" ]]; then
			break
		elif [[ $key == "r" ]]; then
			systemctl reboot
			break
		elif [[ $key == "p" ]]; then
			systemctl poweroff
			break
		fi
	done

system-clean:
	#!/usr/bin/env bash
	podman image prune -af
	podman volume prune -f
	sudo docker image prune -af
	sudo docker volume prune -f
	flatpak uninstall --unused --noninteractive --assumeyes

[no-exit-message]
firmware-update:
	#!/usr/bin/env bash
	while true; do
		read -n 1 -s -r -p $'\e[31mMight require multiple reboots, continue? (y)es, (n)o \e[0m' key
		if [[ $key == "y" ]]; then
			fwupdmgr refresh --force
			fwupdmgr update
			break
		elif [[ $key == "n" ]]; then
			break
		fi
	done

changelogs:
	#!/usr/bin/env bash
	rpm-ostree db diff --changelogs

bios-info:
	#!/usr/bin/env bash
	echo "Manufacturer: $(sudo dmidecode -s baseboard-manufacturer)"
	echo "Product Name: $(sudo dmidecode -s baseboard-product-name)"
	echo "Version: $(sudo dmidecode -s bios-version)"
	echo "Release Date: $(sudo dmidecode -s bios-release-date)"

setup-libvirt-group:
	#!/usr/bin/env bash
	sudo groupadd --system -f libvirt
	sudo usermod -a -G libvirt "$(whoami)"

install-steam:
	#!/usr/bin/env fish
	function install-package
		flatpak --noninteractive --assumeyes install $argv[1]
	end
	install-package com.valvesoftware.Steam
	install-package org.freedesktop.Platform.VulkanLayer.MangoHud
	install-package org.freedesktop.Platform.VulkanLayer.gamescope

install-recommended-fisher-plugins:
	#!/usr/bin/env fish
	source /usr/share/fish/vendor_conf.d/fisher.fish
	function install-plugin
		fisher install $argv[1]
	end
	#install-plugin IlanCosman/tide@v6
	install-plugin blackjid/plugin-kubectl
	install-plugin halostatue/fish-chezmoi
	install-plugin jorgebucaran/autopair.fish
	install-plugin jorgebucaran/getopts.fish
	install-plugin jorgebucaran/replay.fish
	install-plugin lewisacidic/fish-git-abbr
	install-plugin nickeb96/puffer-fish
	install-plugin jorgebucaran/hydro
	#install-plugin meaningful-ooo/sponge


install-recommended-misetools:
	#!/usr/bin/env bash
	function implement() {
		mise install "${1}"@latest
		mise use -g "${1}"@latest
	}

	implement age
	implement calicoctl
	implement flux2
	implement helm
	implement kubectl
	implement kubectx
	implement kubens
	implement kubie
	implement kustomize
	implement sops
	implement talosctl
	implement teleport-community
	implement velero

show-toolbox-examples:
	#!/usr/bin/env bash
	echo "Example for Images:"
	echo "toolbox create -c rhel-8 -i registry.access.redhat.com/ubi8/toolbox:latest"
	echo "toolbox create -c rhel-9 -i registry.access.redhat.com/ubi9/toolbox:latest"
	echo "toolbox create -c rhel-10 -i registry.access.redhat.com/ubi10/toolbox:latest"
	echo "toolbox create -c fedora-41 -i registry.fedoraproject.org/fedora-toolbox:41"

show-distrobox-examples:
	#!/usr/bin/env bash
	echo "Example for Images:"
	echo "distrobox create -n alma -i quay.io/toolbx-images/almalinux-toolbox:latest"
	echo "distrobox create -n arch -i ghcr.io/ublue-os/arch-toolbox:latest"
	echo "distrobox create -n centos -i quay.io/toolbx-images/centos-toolbox:latest"
	echo "distrobox create -n debian -i quay.io/toolbx-images/debian-toolbox:unstable"
	echo "distrobox create -n fedora -i ghcr.io/ublue-os/fedora-toolbox:latest"
	echo "distrobox create -n opensuse -i quay.io/toolbx-images/opensuse-toolbox:tumbleweed"
	echo "distrobox create -n ubuntu -i ghcr.io/ublue-os/ubuntu-toolbox:latest"

set-hostname:
	#!/usr/bin/env bash
	while true; do
		read -p "New hostname: " answer
		if [[ $answer == "" ]]; then
			echo "Error: Empty string"
			exit 1
		else
		    break
		fi
	done
	sudo hostnamectl hostname "${answer}"
